buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.0'
    }
}

apply plugin: 'android-library'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.joda:joda-convert:1.6'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    sourceSets {
        main.manifest.srcFile 'library/AndroidManifest.xml'
        main.java.srcDir 'library/src'
        main.res.srcDir 'library/res'
        main.assets.srcDir 'library/assets'
        instrumentTest.setRoot('test/src/test')
    }

    lintOptions {
        lintConfig file("library/lint.xml")
    }
}

apply from: 'https://raw.github.com/dlew/gradle-mvn-push/master/gradle-mvn-push.gradle'

task updateTzData(dependsOn:assembleDebug, type: JavaExec) {
    main = "org.joda.time.tz.ZoneInfoCompiler"
    classpath = files("build/bundles/debug/classes.jar")
    systemProperty "org.joda.time.DateTimeZone.Provider", "org.joda.time.tz.UTCProvider"
    args "-src", "tzdata", "-dst", "library/res/raw", "africa",  "antarctica", "asia", "australasia", "europe", "northamerica", "southamerica", "pacificnew", "etcetera", "backward", "systemv"
}
